dependencies {
  compile ("org.asciidoctor:asciidoctorj:$asciidoctorJVersion") {
    exclude group:'org.jruby'
  }

  compile "org.jruby:jruby-complete:$jrubyVersion"


  gems("rubygems:asciidoctor-pdf:$asciidoctorPdfGemVersion") {
    // Exclude gems provided by AsciidoctorJ core
    exclude module: 'asciidoctor'
    exclude module: 'thread_safe'
    // We must lock Prawn to 1.3.0 until AsciidoctorJ upgrades to JRuby 9.0.0.0
    exclude module: 'prawn'
    exclude module: 'addressable'
    exclude module: 'public_suffix'
  }
  gems "rubygems:thread_safe:$threadSafeGemVersion"
  
  gems "rubygems:prawn:$prawnGemVersion"
  gems "rubygems:rouge:$rougeGemVersion"
  gems "rubygems:addressable:$addressableVersion"
  gems "rubygems:public_suffix:$public_suffixVersion"

  testCompile "org.apache.pdfbox:pdfbox:$pdfboxVersion"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/*/data/fonts/**"
  include "gems/asciidoctor-pdf-$asciidoctorPdfGemVersion/data/**"
  // Accomodate Prawn's non-conforming packaging
  include "gems/prawn-*/VERSION"
  include "gems/prawn-*/data/*.txt"
  include "gems/prawn-*/data/encodings/*"
  // Include icon fonts
  include "gems/prawn-icon-*/fonts/*/*"
  // Include required data file from the addressable gem
  include "gems/addressable-*/data/*.data"

  exclude 'gems/rouge-*/lib/rouge/demos/**'
}

jrubyPrepare << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    into sourceSets.main.output.resourcesDir
  }
}
