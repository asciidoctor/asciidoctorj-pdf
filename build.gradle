/*
    adding the plugin jars to the classpath to apply them later.
    currently the new plugins DSL does apply them directly.
    there are other limitations too. See https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
    we don't need to apply the jruby and bintray plugin on the rootProject.
*/
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  }
}

// modern plugins config
plugins {
  id "signing"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
  id 'com.github.jruby-gradle.base' version '2.0.0'
}

// TIP use -PpublishRelease=true to active release behavior regardless of the version
status = project.hasProperty('publishRelease') && project.publishRelease.toBoolean() ?
    'release' : ((version == 'unspecified' || version.endsWith('-SNAPSHOT')) ? 'snapshot' : 'release')

ext {
  buildDateTime = new Date()
  (buildDateOnly, buildTimeOnly) = new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss.SSSZ').format(buildDateTime).split(' ')
  statusIsRelease = (status == 'release')

  // jar versions
  arquillianVersion = '1.1.10.Final'
  arquillianSpockVersion = '1.0.0.Beta3'
  jrubyVersion = '9.4.0.0'
  pdfboxVersion = '1.8.16'
  junitVersion = '4.13.2'
  hamcrestVersion = '2.2'

  // gem versions
  asciidoctorJVersion = project.hasProperty('asciidoctorJVersion') ? project.asciidoctorJVersion : '2.5.7'
  asciidoctorPdfGemVersion = project.hasProperty('asciidoctorPdfGemVersion') ? project.asciidoctorPdfGemVersion : '2.3.5'

  addressableVersion = '2.8.0'
  concurrentRubyVersion = '1.1.7'
  public_suffixVersion = '1.4.6'
  prawnGemVersion=project.hasProperty('prawnGemVersion') ? project.prawnGemVersion : '2.4.0'
  rghostGemVersion = '0.9.7'
  spockVersion = '0.7-groovy-2.0'
  threadSafeGemVersion = '0.3.6'
  ttfunkGemVersion = '1.7.0'
  cssParserGemVersion = '1.12.0'
  textHyphenVersion='1.4.1'
}

allprojects {
  group = 'org.asciidoctor'
  defaultTasks 'check'
}

subprojects {
  // NOTE applying Java plugin changes the status; take steps to preserve value
  def _status = status
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply from: "$rootDir/gradle/providedConfiguration.gradle"

  status = _status

  // NOTE sourceCompatibility & targetCompatibility are set in gradle.properties to meet requirements of Gradle
  // Must redefine here to work around a bug in the Eclipse plugin
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

  plugins.withType(JavaPlugin) {
    project.tasks.withType(JavaCompile) { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 8
      }
      if (project.hasProperty("showDeprecation")) {
        options.compilerArgs << "-Xlint:deprecation"
      }
      if (project.hasProperty("showUnchecked")) {
        options.compilerArgs << "-Xlint:unchecked"
      }
    }
    project.tasks.withType(GroovyCompile) { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 8
      }
    }
  }

  repositories {
    if (project.hasProperty('useMavenLocal') && project.useMavenLocal.toBoolean()) {
      mavenLocal()
    }

    mavenCentral()
  }

  dependencies {
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testImplementation "org.jboss.arquillian.junit:arquillian-junit-container:$arquillianVersion"
    testImplementation "org.jboss.arquillian.spock:arquillian-spock-container:$arquillianSpockVersion"
  }

  test {
    forkEvery = 10
    minHeapSize = '128m'
    maxHeapSize = '1024m'
    if (JavaVersion.current().isJava8Compatible()) {
      jvmArgs '-XX:-UseGCOverheadLimit'
    } else {
      jvmArgs '-XX:MaxPermSize=256m', '-XX:-UseGCOverheadLimit'
    }

    testLogging {
      exceptionFormat = 'full'
      // events 'passed', 'failed', 'skipped', 'standard_out', 'standard_error'
      // events 'standard_out', 'standard_error'
      afterSuite { desc, result ->
        if (!desc.parent && logger.infoEnabled) {
          logger.info "Test results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        }
      }
    }
  }

}

configure(subprojects.findAll {it.name != 'itest'}) {
  apply from: rootProject.file('gradle/versioncheck.gradle')

  apply plugin: 'com.github.jruby-gradle.base'

  repositories {
    ruby.gems()
  }

  if (JavaVersion.current().isJava8Compatible()) {
    javadoc {
      // Oracle JDK8 likes to fail the build over spoiled HTML
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  ext {
    // path to use for the prepared jruby gems
    preparedGems = new File("$buildDir/preparedGems")
  }

  sourceSets {
    main {
      //let's register an output folder on the main SourceSet:
      output.dir(preparedGems, builtBy: 'jrubyPrepare')
      //it is now a part of the 'main' classpath and will be a part of the jar
    }
  }

}

configure(subprojects.findAll { it.name != 'itest'}) {

  jruby {
    jrubyVersion = jrubyVersion
  }

  compileJava {
    options.release = 8
  }

  // QUESTION is this the right place to insert this task dependency in the lifecycle?
  // IMPORTANT The TMP or TEMP environment variable must be set for the gem install command to work on Windows
  processResources.dependsOn jrubyPrepare
}

nexusPublishing {
  repositories {
    sonatype {
      if (project.hasProperty("sonatypeUsername")) {
        username = project.sonatypeUsername
      }
      if (project.hasProperty("sonatypePassword")) {
        password = project.sonatypePassword
      }
      repositoryDescription = "Release ${project.group} ${project.version}"
    }
  }
  clientTimeout = Duration.ofMinutes(5)
}
